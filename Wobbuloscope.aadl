package Wobbuloscope
public
	--with Base_Types;
	with Data_Model;
	
	---
	
	data AnalogSignal
	end AnalogSignal;

	data Sample
		properties
       		Data_Model::Data_Representation => Integer;
	end Sample;

	bus USB
	end USB;
	
	bus WiredConnection
	end WiredConnection;
	
	processor XilinxFPGA
		features
			usb: requires bus access USB;
			dac: requires bus access WiredConnection;
			adc1: requires bus access WiredConnection;
			adc2: requires bus access WiredConnection;
	end XilinxFPGA;
	
	processor PhysicalKnob
		features
			wire: requires bus access WiredConnection;
	end PhysicalKnob;

	device Oscilloscope
		features
			analogProbe: in data port AnalogSignal;
			digitalOut: out data port Sample;
			wobbulatorIn: in data port AnalogSignal;
	end Oscilloscope;

	device ControlKnob
		features
			level: out data port AnalogSignal;
	end ControlKnob;
	
	device Wobbulator
		features
			analogOut: out data port AnalogSignal;
			frequencyKnob: in data port AnalogSignal;
			amplitudeKnob: in data port AnalogSignal;
			sampleInput: in data port Sample;
	end Wobbulator;
	
	thread WobbulatorThread
		features
			sampleInput: in data port Sample;
			frequencyKnob: in data port AnalogSignal;
			amplitudeKnob: in data port AnalogSignal;
			analogOut: out data port AnalogSignal;
	end WobbulatorThread;

	thread implementation WobbulatorThread.impl
	end WobbulatorThread.impl;
	
	
	device TestedDevice
		features
			signalIn: in data port AnalogSignal;
			probedSignal: out data port AnalogSignal;
	end TestedDevice;

	device implementation ControlKnob.impl
	end ControlKnob.impl;

	device implementation Oscilloscope.impl
	end Oscilloscope.impl;
	
	device implementation Wobbulator.impl
	end Wobbulator.impl;
	
	device implementation TestedDevice.impl
	end TestedDevice.impl;

	system Wobbuloscope
		features
			sampleOutput: out data port Sample;
			wobbulatorWaveformIn: in data port Sample;
	end Wobbuloscope;

	system implementation Wobbuloscope.impl
		subcomponents
			oscilloscope: device Oscilloscope.impl;
			frequencyKnob: device ControlKnob.impl;
			amplitudeKnob: device ControlKnob.impl;
			wobbulator: device Wobbulator.impl;
			testedDevice: device TestedDevice.impl;
			
			oscilloscopeFpga: processor XilinxFPGA;
			wobbulatorFpga: processor XilinxFPGA;
			freqKnobProc: processor PhysicalKnob;
			amplKnobProc: processor PhysicalKnob;
			
			oscProbeWire: bus WiredConnection;
			generatedSignalWire: bus WiredConnection;
			
			freqKnobWire: bus WiredConnection;
			amplKnobWire: bus WiredConnection;

			externalUsb: bus USB;
		connections
			wobToOsc: port wobbulator.analogOut -> oscilloscope.wobbulatorIn;
			wobToDevice: port wobbulator.analogOut -> testedDevice.signalIn;
			oscilloscopeProbe: port testedDevice.probedSignal -> oscilloscope.analogProbe;
			freqKnob: port frequencyKnob.level -> wobbulator.frequencyKnob;
			amplKnob: port amplitudeKnob.level -> wobbulator.amplitudeKnob;

			oscOut: port oscilloscope.digitalOut -> sampleOutput;
			wobWave: port wobbulatorWaveformIn -> wobbulator.sampleInput;
			
			wireFreq: bus access freqKnobProc.wire -> freqKnobWire;
			wireFreq2: bus access freqKnobWire -> wobbulatorFpga.adc2;
			wireAmpl: bus access amplKnobProc.wire -> amplKnobWire;
			wireAmpl2: bus access amplKnobWire -> wobbulatorFpga.adc1;
			wireOsc1: bus access oscProbeWire -> oscilloscopeFpga.adc1;
			wireOsc2: bus access generatedSignalWire -> oscilloscopeFpga.adc2;
			wireWob1: bus access generatedSignalWire -> wobbulatorFpga.dac;
			
			usbOsc: bus access externalUsb -> oscilloscopeFpga.usb;
			usbWob: bus access externalUsb -> wobbulatorFpga.usb;
		properties
		  Actual_Connection_Binding => (reference(externalUsb)) applies to oscOut;
		  Actual_Connection_Binding => (reference(externalUsb)) applies to wobWave;
		  
		  Actual_Connection_Binding => (reference(freqKnobWire)) applies to freqKnob;
		  Actual_Connection_Binding => (reference(amplKnobWire)) applies to amplKnob;
		  Actual_Connection_Binding => (reference(oscProbeWire)) applies to oscilloscopeProbe;
		  Actual_Connection_Binding => (reference(generatedSignalWire)) applies to wobToOsc;
		  Actual_Connection_Binding => (reference(generatedSignalWire)) applies to wobToDevice;

		  Actual_Processor_Binding => (reference(oscilloscopeFpga)) applies to oscilloscope;
		  Actual_Processor_Binding => (reference(wobbulatorFpga)) applies to wobbulator;
		  Actual_Processor_Binding => (reference(freqKnobProc)) applies to frequencyKnob;
		  Actual_Processor_Binding => (reference(amplKnobProc)) applies to amplitudeKnob;
	end Wobbuloscope.impl;
end Wobbuloscope;