package Wobbuloscope
public
	--with Base_Types;
	with Data_Model;
	
	---
	
	data AnalogSignal
	end AnalogSignal;

	data Sample
		properties
       		Data_Model::Data_Representation => Integer;
	end Sample;
	
	data ControlSignal
	end ControlSignal;

	bus USB
	end USB;
	
	bus WiredConnection
	end WiredConnection;
	
	processor XilinxFPGA
		features
			usb: requires bus access USB;
			dac: requires bus access WiredConnection;
			adc1: requires bus access WiredConnection;
			adc2: requires bus access WiredConnection;
	end XilinxFPGA;
	
	processor PhysicalKnob
		features
			wire: requires bus access WiredConnection;
	end PhysicalKnob;

--	device Oscilloscope
--		features
--			analogProbe: in data port AnalogSignal;
--			digitalOut: out data port Sample;
--			wobbulatorIn: in data port AnalogSignal;
--	end Oscilloscope;

	device ControlKnob
		features
			level: out data port AnalogSignal;
	end ControlKnob;
	
--	device Wobbulator
--		features
--			analogOut: out data port AnalogSignal;
--			frequencyKnob: in data port AnalogSignal;
--			amplitudeKnob: in data port AnalogSignal;
--			sampleInput: in data port Sample;
--	end Wobbulator;
	
	thread WobbulatorControlThread
		features
			frequency: in data port AnalogSignal;
			amplitude: in data port AnalogSignal;
			control: out data port ControlSignal;
	end WobbulatorControlThread;
	
	thread implementation WobbulatorControlThread.impl
	end WobbulatorControlThread.impl;
	
	thread WobbulatorSignalThread
		features
			control: in data port ControlSignal;
			waveform: in data port Sample;
			generatedSignal: out data port AnalogSignal;
	end WobbulatorSignalThread;
	
	thread implementation WobbulatorSignalThread.impl
	end WobbulatorSignalThread.impl;
	
	process WobbulatorProcess
		features
			sampleInput: in data port Sample;
			frequencyKnob: in data port AnalogSignal;
			amplitudeKnob: in data port AnalogSignal;
			analogOut: out data port AnalogSignal;
	end WobbulatorProcess;

	process implementation WobbulatorProcess.impl
		subcomponents
			controlThread: thread WobbulatorControlThread.impl;
			signalThread: thread WobbulatorSignalThread.impl;
		connections
			ampl: port amplitudeKnob -> controlThread.amplitude;
			freq: port frequencyKnob -> controlThread.frequency;
			ctl: port controlThread.control -> signalThread.control;
			inp: port sampleInput -> signalThread.waveform;
			outp: port signalThread.generatedSignal -> analogOut;
	end WobbulatorProcess.impl;
	
	thread OscilloscopeSampleThread
		features
			wobbulatorSignal: in data port AnalogSignal;
			deviceSignal: in data port AnalogSignal;
			sampleOut: out data port Sample;
	end OscilloscopeSampleThread;
	
	thread implementation OscilloscopeSampleThread.impl
	end OscilloscopeSampleThread.impl;

	process OscilloscopeProcess
		features
			deviceInput: in data port AnalogSignal;
			wobbulatorInput: in data port AnalogSignal;
			sampleOut: out data port Sample;	
	end OscilloscopeProcess;
	
	process implementation OscilloscopeProcess.impl
		subcomponents
			signalThread: thread OscilloscopeSampleThread.impl;
		connections
			sig: port deviceInput -> signalThread.deviceSignal;
			wob: port wobbulatorInput -> signalThread.wobbulatorSignal;
			samp: port signalThread.sampleOut -> sampleOut; 
	end OscilloscopeProcess.impl;
	
	device TestedDevice
		features
			signalIn: in data port AnalogSignal;
			probedSignal: out data port AnalogSignal;
	end TestedDevice;

	device implementation ControlKnob.impl
	end ControlKnob.impl;

--	device implementation Oscilloscope.impl
--	end Oscilloscope.impl;
--	
--	device implementation Wobbulator.impl
--	end Wobbulator.impl;
	
	device implementation TestedDevice.impl
	end TestedDevice.impl;

	system Wobbuloscope
		features
			sampleOutput: out data port Sample;
			wobbulatorWaveformIn: in data port Sample;
	end Wobbuloscope;

	system implementation Wobbuloscope.impl
		subcomponents
--			oscilloscope: device Oscilloscope.impl;
			frequencyKnob: device ControlKnob.impl;
			amplitudeKnob: device ControlKnob.impl;
--			wobbulator: device Wobbulator.impl;
			testedDevice: device TestedDevice.impl;
			
			oscilloscopeFpga: processor XilinxFPGA;
			wobbulatorFpga: processor XilinxFPGA;
			freqKnobProc: processor PhysicalKnob;
			amplKnobProc: processor PhysicalKnob;
			
			oscilloscopeProcess: process OscilloscopeProcess.impl;
			wobbulatorProcess: process WobbulatorProcess.impl;
			
			oscProbeWire: bus WiredConnection;
			generatedSignalWire: bus WiredConnection;
			
			freqKnobWire: bus WiredConnection;
			amplKnobWire: bus WiredConnection;

			externalUsb: bus USB;
		connections
			wobToOsc: port wobbulatorProcess.analogOut -> oscilloscopeProcess.wobbulatorInput;
			wobToDevice: port wobbulatorProcess.analogOut -> testedDevice.signalIn;
			oscilloscopeProbe: port testedDevice.probedSignal -> oscilloscopeProcess.deviceInput;
			freqKnob: port frequencyKnob.level -> wobbulatorProcess.frequencyKnob;
			amplKnob: port amplitudeKnob.level -> wobbulatorProcess.amplitudeKnob;

			oscOut: port oscilloscopeProcess.sampleOut -> sampleOutput;
			wobWave: port wobbulatorWaveformIn -> wobbulatorProcess.sampleInput;
			
			wireFreq: bus access freqKnobProc.wire -> freqKnobWire;
			wireFreq2: bus access freqKnobWire -> wobbulatorFpga.adc2;
			wireAmpl: bus access amplKnobProc.wire -> amplKnobWire;
			wireAmpl2: bus access amplKnobWire -> wobbulatorFpga.adc1;
			wireOsc1: bus access oscProbeWire -> oscilloscopeFpga.adc1;
			wireOsc2: bus access generatedSignalWire -> oscilloscopeFpga.adc2;
			wireWob1: bus access generatedSignalWire -> wobbulatorFpga.dac;
			
			usbOsc: bus access externalUsb -> oscilloscopeFpga.usb;
			usbWob: bus access externalUsb -> wobbulatorFpga.usb;
		properties
		  Actual_Connection_Binding => (reference(externalUsb)) applies to oscOut;
		  Actual_Connection_Binding => (reference(externalUsb)) applies to wobWave;
		  
		  Actual_Connection_Binding => (reference(freqKnobWire)) applies to freqKnob;
		  Actual_Connection_Binding => (reference(amplKnobWire)) applies to amplKnob;
		  Actual_Connection_Binding => (reference(oscProbeWire)) applies to oscilloscopeProbe;
		  Actual_Connection_Binding => (reference(generatedSignalWire)) applies to wobToOsc;
		  Actual_Connection_Binding => (reference(generatedSignalWire)) applies to wobToDevice;

--		  Actual_Processor_Binding => (reference(oscilloscopeFpga)) applies to oscilloscope;
--		  Actual_Processor_Binding => (reference(wobbulatorFpga)) applies to wobbulator;
		  Actual_Processor_Binding => (reference(freqKnobProc)) applies to frequencyKnob;
		  Actual_Processor_Binding => (reference(amplKnobProc)) applies to amplitudeKnob;
		  
		  Actual_Processor_Binding => (reference(oscilloscopeFpga)) applies to oscilloscopeProcess;
		  Actual_Processor_Binding => (reference(wobbulatorFpga)) applies to wobbulatorProcess;
	end Wobbuloscope.impl;
end Wobbuloscope;